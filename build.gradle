buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
        springPlatformVersion = 'Cairo-SR3'
        kotlinVersion = '1.2.41'
        xodusVersion = '1.2.3'

        repos = {
            mavenLocal()
            mavenCentral()
            maven { url 'http://repo.maven.apache.org/maven2' }
            maven { url 'http://repo.spring.io/plugins-release' }
            maven { url 'https://plugins.gradle.org/m2/' }
        }
    }

    repositories repos

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE'
        classpath 'io.spring.gradle:propdeps-plugin:0.0.8'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.21"
        classpath "net.rdrei.android.buildtimetracker:gradle-plugin:0.11.0"
    }
}

apply plugin: 'build-time-tracker'

buildtimetracker {
    reporters {
        summary {
            ordered true
        }
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: "kotlin-kapt"
    apply plugin: 'kotlin-spring'

    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'

    apply plugin: 'io.spring.dependency-management'

    apply plugin: "com.gorylenko.gradle-git-properties"

    group = 'org.relumin'
    version = '1.0.0-SNAPSHOT'

    sourceCompatibility = 1.8

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
            javaParameters = true
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
            javaParameters = true
        }
    }

    repositories repos

    // Basically, we manage by spring platform BOM.
    dependencyManagement {
        imports {
            mavenBom "io.spring.platform:platform-bom:${springPlatformVersion}"
        }

        // except spring platform-bom library versions
        dependencies {
            // except spring platform-bom library versions
            [
                    // kotlin
                    "org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlinVersion}",
                    "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}",

                    // xodus
                    "org.jetbrains.xodus:xodus-openAPI:${xodusVersion}",
                    "org.jetbrains.xodus:xodus-entity-store:${xodusVersion}",
            ].each { dependency it }
        }
    }

    dependencies {
        compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        compile 'org.jetbrains.kotlin:kotlin-reflect'
        compile 'com.fasterxml.jackson.module:jackson-module-kotlin'

        compile 'org.springframework.boot:spring-boot-starter'
        kapt "org.springframework.boot:spring-boot-configuration-processor"
        compileOnly "org.springframework.boot:spring-boot-configuration-processor"
        testCompile 'org.springframework.boot:spring-boot-starter-test'
    }

    compileJava.dependsOn(processResources)
}
